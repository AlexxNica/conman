#!/usr/bin/expect -f
###############################################################################
# $Id$
###############################################################################
# Copyright (C) 2001-2007 The Regents of the University of California.
# Produced at Lawrence Livermore National Laboratory.
# Written by Chris Dunlap <cdunlap@llnl.gov>.
# UCRL-CODE-2002-009.
#
# This file is part of ConMan: The Console Manager.
# For details, see <http://home.gna.org/conman/>.
###############################################################################
# This script connects to a console using the IPMI Serial-Over-LAN protocol.
# The "ipmiconsole" command is part of GNU FreeIPMI available from
# <http://savannah.gnu.org/projects/freeipmi/>.
#
# This script can be specified in "conman.conf" in the following manner:
#
#   CONSOLE name="zot" dev="/path/to/ipmiconsole.exp HOST USER PSWD [KEY]"
#
# HOST is the hostname of the remote server.
# USER is the username being authenticated.
# PSWD is the corresponding password.
# KEY  is the optional key-generation key "Kg" for IPMIv2 authentication.
#
# Since this command-line will persist in the process listing for the duration
# of the connection, passing PSWD or KEY in this manner is not recommended.
###############################################################################

##
# Set "exp_internal" to 1 to print diagnostics describing internal operations.
#   This is helpful in diagnosing pattern-match failures.
##
  exp_internal 0

##
# Set "log_user" to 1 to show the underlying dialogue that takes place during
#   the initial authentication step.
##
  log_user 0

##
# The "timeout" specifies the number of seconds before the authentication step
#   times-out and terminates the connection.
##
  set timeout 5

##
# Command-line argument defaults should be specified here.  In this manner,
#   you can avoid exposing sensitive information on the command line.
##
# set user "foo"
# set pswd "bar"
# set key  "qux"

##

set env(PATH) "/usr/sbin:/usr/local/sbin:/usr/bin:/usr/local/bin"
set authenticated 0
set keyed 1

if { ! $argc } {
  set prog [ lindex [ split $argv0 "/" ] end ]
  send_user "Usage: $prog <host> <user> <pswd> \[<key>]\r\n"
  exit 1
}
if { $argc > 0 } {
  set host [ lindex $argv 0 ]
}
if { $argc > 1 } {
  set user [ lindex $argv 1 ]
}
if { $argc > 2 } {
  set pswd [ lindex $argv 2 ]
}
if { $argc > 3 } {
  set key  [ lindex $argv 3 ]
}
if { ! [ info exists host ] } {
  send_user "Error: Unspecified hostname.\r\n"
  exit 1
} elseif { ! [ info exists user ] } {
  send_user "Error: Unspecified username.\r\n"
  exit 1
} elseif { ! [ info exists pswd ] } {
  send_user "Error: Unspecified password.\r\n"
  exit 1
}
set cmd [ list ipmiconsole -h $host -u $user -P ]
if { [ info exists key ] } {
  lappend cmd -K
  set keyed 0
}
if [ catch "spawn $cmd" spawn_result ] {
  send_user "Error: $spawn_result.\r\n"
  exit 1
}
expect {
  -nocase -re "\\\[SOL established]" {
    ;
  }
  -nocase -re "^\\\[error received]: (\[^\r\n]*)" {
    send_user "Error: $expect_out(1,string)\r\n"
    exit 1
  }
  eof {
    send_user "Error: Connection closed by remote host.\r\n"
    exit 1
  }
  timeout {
    if {($authenticated == 0) || ($keyed == 0)} {
      send_user "Error: Timed-out.\r\n"
      exit 1
    }
  }
  -nocase -gl "Password:" {
    if {$authenticated == 0} {
      send "$pswd\r"
      incr authenticated
      exp_continue -continue_timer
    }
  }
  -nocase -gl "K_g:" {
    if {$keyed == 0} {
      send "$key\r"
      incr keyed
      exp_continue -continue_timer
    }
  }
  -re "\[^\r]*\r\n" {
    exp_continue -continue_timer
  }
}
send_user "Connection established via external ipmiconsole process (pid $spawn_result).\r\n"
interact
