#!/usr/bin/expect --

##
# $Id: boot_alpha.exp,v 1.4 2001/08/23 17:59:42 dun Exp $
#   by Chris Dunlap <cdunlap@llnl.gov>
##

set env(PATH) "/bin:/usr/bin:/usr/local/bin"
source /usr/lib/conman/conman.exp
source /usr/lib/conman/alpha.exp
log_user 0

proc pre_boot_alpha {spawn_id output_id console} {

  exec powerman -r $console
  if {! [alpha_is_at_srm $spawn_id $output_id $console 150]} {
    return 0
  }
  set cmds [list \
    {set boot_osflags "ip=dhcp root=/dev/nfs console=ttyS0"} \
    {set eib0_mode Auto-Negotiate} \
    {set eib0_protocols BOOTP} \
    {set boot_reset OFF} \
    {set auto_action halt} \
    {set bootdef_dev eib0} \
  ]
  foreach cmd $cmds {
    alpha_do_srm_cmd $spawn_id $output_id $console 0 $cmd
  }
  return 1
}

set consoles [conman_query $argv]
set n [llength $consoles]
if {$n == 0} {
  send_error "Found no matching consoles.\n"
  exit 1
} elseif {$n == 1} {
  send_user "Resetting console [lindex $consoles 0].\n"
} else {
  send_user "Resetting $n consoles.\n"
}

conman_run 128 "-j $argv" pre_boot_alpha

foreach console $consoles {
  send_user "Booting $console.\n"
  conman_run 1 "-j $console" alpha_do_srm_cmd 0 "boot" 10
}

exit 0
