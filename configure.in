dnl Process this file with autoconf to produce a configure script.
dnl
dnl ConMan AutoConf Config Jazz
dnl   by Chris Dunlap <cdunlap@llnl.gov>
dnl
dnl $Id: configure.in,v 1.4 2001/05/29 23:45:24 dun Exp $


dnl Preamble:  We, the directives of the autoconf file,
dnl   in order to build a more portable code, ...
dnl
AC_INIT(common.h)
AC_CONFIG_AUX_DIR(auxdir)
AC_CONFIG_HEADER(config.h)


dnl Read package and version info from VERSION.
dnl
PACKAGE="`sed -ne 's/^\(.*\)-\(.*\)/\1/p' $srcdir/VERSION`"
AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE", [Define the package name.])
AC_SUBST(PACKAGE)
VERSION="`sed -ne 's/^\(.*\)-\(.*\)/\2/p' $srcdir/VERSION`"
AC_DEFINE_UNQUOTED(VERSION, "$VERSION", [Define the package version.])
AC_SUBST(VERSION)


dnl Determine the system type.
dnl
AC_CANONICAL_HOST
AC_SUBST(host_cpu)
AC_DEFINE_UNQUOTED(HOST_CPU, "$host_cpu",
  [Define the canonical host CPU type.])
AC_SUBST(host_os)
AC_DEFINE_UNQUOTED(HOST_OS, "$host_os",
  [Define the canonical host OS type.])
AC_SUBST(host_vendor)
AC_DEFINE_UNQUOTED(HOST_VENDOR, "$host_vendor",
  [Define the canonical host vendor type.])
AC_SUBST(host_alias)
AC_DEFINE_UNQUOTED(HOST_ALIAS, "$host_alias",
  [Define the canonical host alias.])


dnl Check for programs.
dnl
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB


dnl Check for libraries.
dnl


dnl Check for header files.
dnl


dnl Check for typedefs.
dnl


dnl Check for structures.
dnl


dnl Check for compiler characteristics.
dnl
AC_C_BIGENDIAN


dnl Check for library functions.
dnl
AC_CHECK_FUNCS( \
  inet_ntop \
  inet_pton \
  localtime_r \
)
AC_REPLACE_FUNCS( \
  strlcat \
  strlcpy \
)


dnl Check for system services.
dnl


dnl Check for system-specific stuff.
dnl


dnl Check for debug vs. production compilation.
dnl
AC_MSG_CHECKING(whether debugging is enabled)
AC_ARG_ENABLE(debug,
[  --enable-debug          turn on debugging for development work],
[case "${enableval}" in
  yes) debug=yes ;;
  no)  debug=no ;;
  *)   AC_MSG_ERROR(bad value ${enableval} for --enable-debug) ;;
esac],
[debug=no])
if test "$debug" = yes; then
  CFLAGS="-ggdb -Wall";
else
  CFLAGS="-O3";
  LDFLAGS="-s";
  AC_DEFINE(NDEBUG, 1, [Define if you are building a production release.])
fi
AC_MSG_RESULT($debug)


dnl Check for random silliness.
dnl
AC_CACHE_CHECK([for a sense of humor], ac_cv_humor, ac_cv_humor=yes)


dnl Add defs to config.h.
dnl
AC_DEFINE(USE_THREAD_SAFE_LIST, 1, [Define if list needs to be thread-safe.])


dnl Epilogue.
dnl
AC_OUTPUT_COMMANDS(echo "creating dependencies"; make depend >/dev/null)
AC_OUTPUT([
  Makefile
])
